---
page_title: "Terraform AKP Provider version 0.9 upgrading guide"
subcategory: ""
description: Terraform AKP Provider Version 0.9 Upgrade Guide
---

# Terraform AKP Provider Version 0.9 Upgrade Guide

Version 0.9.0 of the AKP provider for Terraform includes changes that you need to consider when upgrading. This guide
will help with that process and focuses on changes from version 0.8.x to version 0.9.0.

## Provider Version Configuration

-> Before upgrading to version 0.9.0, upgrade to the most recent 0.8.x version (0.8.4) of the provider and ensure that
your environment successfully runs [`terraform plan`](https://www.terraform.io/docs/commands/plan.html). You should not
see changes you don't expect or deprecation notices. Also, back up your `.tf` and `.tfstate` files prior to starting the
upgrade process.

Use [version constraints when configuring Terraform providers](https://www.terraform.io/docs/configuration/providers.html#provider-versions).
If you are following that recommendation, update the version constraints in your Terraform configuration and
run [`terraform init -upgrade`](https://www.terraform.io/docs/commands/init.html) to download the new version.

For example, given this previous configuration:

```terraform
terraform {
  required_providers {
    akp = {
      source  = "akuity/akp"
      version = "~> 0.8.4"
    }
  }
}

provider "akp" {
  # Configuration options
}
```

Update to the latest 0.9.0 version:

```terraform
terraform {
  required_providers {
    akp = {
      source  = "akuity/akp"
      version = "~> 0.9.0"
    }
  }
}

provider "akp" {
  # Configuration options
}
```

## akp_instance

### New Fields

Version 0.9.0 introduces the following new fields on `akp_instance` resource and data source:

| Field                                                              | Description                                                                                                                                                                                                 |
|--------------------------------------------------------------------|-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
| `argocd.spec.instance_spec.app_in_any_namespace_config (Attributes)` | (Optional) Configuration to enable applications in any namespace. When enabled, allows ArgoCD applications to be created in namespaces other than the ArgoCD namespace.                                    |
| `argocd_resources (Map)`                                           | (Optional) Manage ArgoCD resources directly within the instance. Accepts a map where keys are resource identifiers and values are JSON-encoded Kubernetes manifests for ArgoCD resources like Applications, Projects, etc. |

#### app_in_any_namespace_config

The `app_in_any_namespace_config` field allows you to enable ArgoCD applications in any namespace:

```terraform
resource "akp_instance" "example" {
  name = "test"
  argocd = {
    spec = {
      instance_spec = {
        app_in_any_namespace_config = {
          enabled = true
        }
        # ... other instance_spec fields
      }
      # ... other spec fields
    }
  }
  # ... other configuration
}
```

#### argocd_resources

The `argocd_resources` field enables you to manage ArgoCD resources directly within your instance configuration. This allows you to define Applications, Projects, and other ArgoCD resources as part of your Terraform configuration:

```terraform
resource "akp_instance" "example" {
  name = "test"
  # ... other configuration
  argocd_resources = local.argocd_resources
}

# Choose a directory that contains argo resource manifests.
# For example, here we have argo.yaml in the argo-manifests directory, and the data is like:
# ---------------------------------------------
# apiVersion: argoproj.io/v1alpha1
# kind: Application
# metadata:
#   name: app1
#   namespace: argocd
# spec:
#   destination:
#     namespace: default
#     server: http://cluster-my-cluster:8001
#   project: default
#   source:
#     path: helm-guestbook
#     repoURL: https://github.com/argoproj/argocd-example-apps.git
#     targetRevision: HEAD
#   syncPolicy:
#     automated: {}
#     syncOptions:
#     - CreateNamespace=true
# ---
# ...
# ---------------------------------------------
#
# The following expression can parse the provided YAMLs into JSON strings for the provider to be validated and applied correctly.
# Remember to put the parsed argo resources into `akp_instance.argocd_resources` field.

locals {
  yaml_files = fileset("${path.module}/argocd-manifests", "*.yaml")

  argocd_resources = merge([
    for file_name in local.yaml_files : {
      for idx, resource_yaml in split("\n---\n", file("${path.module}/argocd-manifests/${file_name}")) :
      "${yamldecode(resource_yaml).apiVersion}/${yamldecode(resource_yaml).kind}/${try(yamldecode(resource_yaml).metadata.namespace, "")}/${yamldecode(resource_yaml).metadata.name}" => jsonencode(yamldecode(resource_yaml))
      if trimspace(resource_yaml) != ""
    }
  ]...)
}
```

## akp_kargo_instance

### New Fields

Version 0.9.0 introduces the following new field on `akp_kargo_instance` resource:

| Field                    | Description                                                                                                                                                                                               |
|--------------------------|-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
| `kargo_resources (Map)`  | (Optional) Manage Kargo resources directly within the instance. Accepts a map where keys are resource identifiers and values are JSON-encoded Kubernetes manifests for Kargo resources like Projects, Warehouses, Stages, etc. |

#### kargo_resources

The `kargo_resources` field enables you to manage Kargo resources directly within your Kargo instance configuration:

```terraform
resource "akp_kargo_instance" "example" {
  name = "test"
  # ... other configuration
  kargo_resources = local.kargo_resources
}

# Choose a directory that contains Kargo resource manifests.
# For example, here we have kargo.yaml in the kargo-manifests directory, and the data is like:
# ---------------------------------------------
# apiVersion: kargo.akuity.io/v1alpha1
# kind: Project
# metadata:
#   name: kargo-demo
# ---
# apiVersion: kargo.akuity.io/v1alpha1
# kind: Warehouse
# metadata:
#   name: kargo-demo
#   namespace: kargo-demo
# spec:
#   subscriptions:
#   - image:
#       repoURL: public.ecr.aws/nginx/nginx
#       semverConstraint: ^1.28.0
#       discoveryLimit: 5
# ---
# ...
# ---------------------------------------------
#
# The following expression can parse the provided YAMLs into JSON strings for the provider to be validated and applied correctly.
# Remember to put the parsed kargo resources into `akp_kargo_instance.kargo_resources` field.
locals {
  yaml_files = fileset("${path.module}/kargo-manifests", "*.yaml")

  kargo_resources = merge([
    for file_name in local.yaml_files : {
      for idx, resource_yaml in split("\n---\n", file("${path.module}/kargo-manifests/${file_name}")) :
      "${yamldecode(resource_yaml).apiVersion}/${yamldecode(resource_yaml).kind}/${try(yamldecode(resource_yaml).metadata.namespace, "")}/${yamldecode(resource_yaml).metadata.name}" => jsonencode(yamldecode(resource_yaml))
      if trimspace(resource_yaml) != ""
    }
  ]...)
}
```

## akp_cluster

### New Fields

Version 0.9.0 introduces enhanced authentication options for `akp_cluster` resource:

| Field                          | Description                                                                                                                                                      |
|--------------------------------|------------------------------------------------------------------------------------------------------------------------------------------------------------------|
| `kube_config.exec (Attributes)` | (Optional) Exec-based authentication configuration for cluster access. Supports external commands like AWS CLI for token generation, enabling more flexible authentication methods. |

#### kube_config.exec

The `exec` authentication method allows you to use external commands for cluster authentication, such as AWS CLI for EKS clusters:

```terraform
resource "akp_cluster" "my-cluster" {
  instance_id = akp_instance.argocd.id
  kube_config = {
    host                   = "https://${cluster.my-cluster.endpoint}"
    cluster_ca_certificate = "${base64decode(cluster.my-cluster.master_auth.0.cluster_ca_certificate)}"
    # Use exec for dynamic token generation
    exec = {
      api_version = "client.authentication.k8s.io/v1"
      args        = ["eks", "get-token", "--cluster-name", "some-cluster"]
      command     = "aws"
      env = {
        AWS_REGION = "us-west-2"
      }
    }
  }
  name      = "my-cluster"
  namespace = "akuity"
  spec = {
    data = {
      size = "small"
    }
  }
}
```

The `exec` configuration supports the following fields:

- `api_version (String)`: API version for the exec credential plugin (typically "client.authentication.k8s.io/v1")
- `command (String)`: Command to execute for authentication
- `args (List)`: Arguments to pass to the command
- `env (Map)`: Environment variables to set when executing the command

## Apply the change

After updating your configuration, run `terraform plan` again to verify no unintended changes were introduced. If you modified existing fields or added new 0.9.0 fields, review the planned changes before running `terraform apply` to apply the updates.

These new features provide enhanced flexibility for managing ArgoCD and Kargo resources directly within your Terraform configuration, as well as improved authentication options for cluster access.
